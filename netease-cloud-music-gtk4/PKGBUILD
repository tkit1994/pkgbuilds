# Maintainer: HaoCheng <ch1994@outlook.com>
pkgname=netease-cloud-music-gtk4
_pkgname=netease-cloud-music-gtk
pkgver=2.0.3
pkgrel=1
pkgdesc="Linux 平台下基于 Rust + GTK4 开发的网易云音乐播放器"
arch=('i686' 'x86_64')
url="https://github.com/gmg137/netease-cloud-music-gtk"
license=('GPL3')
depends=(
	'libadwaita'
	'gst-plugins-base'
)
optdepends=(
	'gst-plugins-bad: extra media codecs'
	'gst-plugins-good: extra media codecs'
	'gst-plugins-ugly: extra media codecs'
 )
makedepends=('cargo' 'meson')
conflicts=(
	'netease-cloud-music-gtk-bin'
	'netease-cloud-music-gtk4-git'
)
source=("https://github.com/gmg137/$_pkgname/archive/$pkgver.tar.gz")
sha256sums=('826556d85e7710e02123cf9cee1b1d295666291c01faff661ef025eb3822d01b')

build() {
	CFLAGS+=" -ffat-lto-objects"
	arch-meson --buildtype release  "$_pkgname-$pkgver" build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}

